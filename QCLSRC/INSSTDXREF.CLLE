/******************************************************************************/
/*                                                                            */
/*  STANDARD CROSS REFERENCE - INSTALLATION SCRIPT                            */
/*                                                                            */
/*  Copyright (C) 2006, 2018  Christopher F. Burns Sr.                        */
/*                                                                            */
/*  This program is free software: you can redistribute it and/or modify      */
/*  it under the terms of the GNU General Public License as published by      */
/*  the Free Software Foundation, either version 3 of the License, or         */
/*  (at your option) any later version.                                       */
/*                                                                            */
/*  This program is distributed in the hope that it will be useful,           */
/*  but WITHOUT ANY WARRANTY; without even the implied warranty of            */
/*  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the             */
/*  GNU General Public License for more details.                              */
/*                                                                            */
/*  You should have received a copy of the GNU General Public License         */
/*  along with this program.  If not, see <http://www.gnu.org/licenses/>.     */
/*                                                                            */
/******************************************************************************/


/******************************************************************************/
/*  HOUSEKEEPING                                                              */
/******************************************************************************/

             PGM        PARM(&INSLIB &USRPRF)

             DCLF       FILE(QADSPOBJ) OPNID(A)
             DCLF       FILE(QAFDMBRL) OPNID(B)

             DCL        VAR(&INSLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&USRPRF) TYPE(*CHAR) LEN(10)
             DCL        VAR(&CURLIB) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SRCSAV) TYPE(*CHAR) LEN(10)
             DCL        VAR(&SRCPTH) TYPE(*CHAR) LEN(64)
             DCL        VAR(&LIBPTH) TYPE(*CHAR) LEN(64)
             DCL        VAR(&MBRPTH) TYPE(*CHAR) LEN(64)
             DCL        VAR(&MSGTXT) TYPE(*CHAR) LEN(128)

             RTVJOBA    CURLIB(&CURLIB)


/******************************************************************************/
/*  CREATE SPECIFIED LIBRARIES AND USER PROFILE.                              */
/******************************************************************************/

             CRTLIB     LIB(&INSLIB) TEXT('Standard Cross Reference')
                        MONMSG     MSGID(CPF0000) EXEC(DO)
                        CHGVAR     VAR(&MSGTXT) VALUE('Installation library' +
                          *BCAT &INSLIB *BCAT 'already exists.')
                        GOTO       CMDLBL(EXIT)
                        ENDDO

 USRPRF:     CRTUSRPRF  USRPRF(&USRPRF) PWDEXP(*YES) USRCLS(*PGMR) +
                          CURLIB(&INSLIB) INLPGM(*NONE) +
                          INLMNU(*SIGNOFF) LMTCPB(*YES)
                        MONMSG     MSGID(CPF0000)

             CHGOBJOWN  OBJ(&INSLIB) OBJTYPE(*LIB) NEWOWN(&USRPRF) +
                          CUROWNAUT(*REVOKE)
                        MONMSG     MSGID(CPF0000)


/******************************************************************************/
/*  RESTORE SOURCE CODE AND MENU OBJECTS, BUILD DATA OBJECTS.                 */
/******************************************************************************/

             CHGCURLIB  CURLIB(&INSLIB)

             ADDLIBLE   LIB(&INSLIB) POSITION(*FIRST)

             RSTOBJ     OBJ(Q*) SAVLIB(STDXREFSRC) DEV(*SAVF) +
                          OBJTYPE(*FILE) SAVF(QTEMP/STDXREFSRC) +
                          MBROPT(*ALL) ALWOBJDIF(*ALL) RSTLIB(&INSLIB)
                        MONMSG     MSGID(CPF0000)

             MOVOBJ     OBJ(QTEMP/STDXREFS) OBJTYPE(*FILE) +
                          TOLIB(&INSLIB)
                        MONMSG     MSGID(CPF0000)

             MOVOBJ     OBJ(QTEMP/STDXREFS) OBJTYPE(*MSGF) +
                          TOLIB(&INSLIB)
                        MONMSG     MSGID(CPF0000)

             RUNSQLSTM  SRCFILE(&INSLIB/QTXTSRC) SRCMBR(STDXREF) +
                          COMMIT(*NONE) NAMING(*SYS) ERRLVL(40) +
                          DFTRDBCOL(*NONE)
                        MONMSG     MSGID(SQL0000)


/******************************************************************************/
/*  VERIFY RPG COMPATIBILITY.                                                 */
/******************************************************************************/

             CRTBNDRPG  PGM(QTEMP/VERIFYRPG) SRCFILE(&INSLIB/QRPGLESRC) +
                          OUTPUT(*NONE)

             MONMSG     MSGID(CPF0000) EXEC(DO)

                        CHGVAR     VAR(&MSGTXT) VALUE('System does not meet OS +
                          release and/or PTF requirements.')

                        GOTO       CMDLBL(EXIT)

                        ENDDO


/******************************************************************************/
/*  BUILD SERVICE PROGRAM FOR STANDARD I/O FUNCTIONS AND BUSINESS RULES.      */
/******************************************************************************/

             CRTBNDDIR  BNDDIR(&INSLIB/STDXREF)
                        MONMSG     MSGID(CPF0000)

             CRTDSPF    FILE(&INSLIB/STDXREFIOP) +
                          SRCFILE(&INSLIB/QDDSSRC) RSTDSP(*YES) +
                          REPLACE(*YES)
                        MONMSG     MSGID(CPF0000)

             CRTSQLRPGI OBJ(&INSLIB/STDXREFIOP) +
                          SRCFILE(&INSLIB/QRPGLESRC) COMMIT(*NONE) +
                          OBJTYPE(*MODULE) RPGPPOPT(*LVL2) +
                          DATFMT(*ISO) REPLACE(*YES) DBGVIEW(*SOURCE)
                        MONMSG     MSGID(CPF0000)

             CRTSRVPGM  SRVPGM(&INSLIB/STDXREFIOP) +
                          SRCFILE(&INSLIB/QSRVSRC) BNDDIR(STDXREF) +
                          ACTGRP(STDXREF) OPTION(*DUPPROC) +
                          REPLACE(*YES) AUT(*ALL)
                        MONMSG     MSGID(CPF0000)

             DLTMOD     MODULE(&INSLIB/STDXREFIOP)
                        MONMSG     MSGID(CPF0000)

             ADDBNDDIRE BNDDIR(&INSLIB/STDXREF) OBJ((STDXREFIOP +
                          *SRVPGM *IMMED))
                        MONMSG     MSGID(CPF0000)


/******************************************************************************/
/*  BUILD SQL USER DEFINED FUNCTIONS ASSOCIATED WITH *SRVPGM PROCEDURES.      */
/******************************************************************************/

             RUNSQLSTM  SRCFILE(&INSLIB/QTXTSRC) SRCMBR(STDXREFSQL) +
                          COMMIT(*NONE) ERRLVL(40)
                        MONMSG     MSGID(SQL0000)


/******************************************************************************/
/*  OTHER MISCELLANEOUS OBJECTS.                                              */
/******************************************************************************/

             CRTDSPF    FILE(&INSLIB/STDXREFMNT) +
                          SRCFILE(&INSLIB/QDDSSRC) RSTDSP(*YES) +
                          REPLACE(*YES)
                        MONMSG     MSGID(CPF0000)

             CRTSQLRPGI OBJ(&INSLIB/STDXREFMNT) +
                          SRCFILE(&INSLIB/QRPGLESRC) COMMIT(*NONE) +
                          OBJTYPE(*PGM) RPGPPOPT(*LVL2) +
                          DATFMT(*ISO) REPLACE(*YES) DBGVIEW(*SOURCE)
                        MONMSG     MSGID(SQL0000)

             CRTMNU     MENU(&INSLIB/STDXREFS) TYPE(*DSPF) +
                          DSPF(&INSLIB/STDXREFS) +
                          MSGF(&INSLIB/STDXREFS) CMDLIN(*LONG) +
                          DSPKEY(*YES) CURLIB(*NOCHG) +
                          PRDLIB(*NOCHG) REPLACE(*YES) +
                          TEXT('Standard Cross Reference Main Menu')
                        MONMSG     MSGID(CPF0000)

             CRTQMQRY   QMQRY(&INSLIB/STDXREFLST) +
                          SRCFILE(&INSLIB/QTXTSRC)
                        MONMSG     MSGID(CPF0000)

             CRTQMFORM  QMFORM(&INSLIB/STDXREFLST) +
                          SRCFILE(&INSLIB/QQMFORMSRC)
                        MONMSG     MSGID(CPF0000)

             CRTBNDCL   PGM(&INSLIB/STDXREFLST) +
                          SRCFILE(&INSLIB/QCLSRC) SRCMBR(STDXREFLST)
                        MONMSG     MSGID(CPF0000)

             CRTCMD     CMD(&INSLIB/STDXREFMNT) +
                          PGM(&INSLIB/STDXREFMNT) +
                          SRCFILE(&INSLIB/QCMDSRC) AUT(*ALL) +
                          REPLACE(*YES)
                        MONMSG     MSGID(CPF0000)

             CRTCMD     CMD(&INSLIB/STDXREFLST) +
                          PGM(&INSLIB/STDXREFLST) +
                          SRCFILE(&INSLIB/QCMDSRC) AUT(*ALL) +
                          REPLACE(*YES)
                        MONMSG     MSGID(CPF0000)

/******************************************************************************/
/*  CHANGE OWNERSHIP OF ALL INSTALLED OBJECTS TO SPECIFIED USER.              */
/******************************************************************************/

             DSPOBJD    OBJ(&INSLIB/*ALL) OBJTYPE(*ALL) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/QADSPOBJ) +
                          OUTMBR(*FIRST *REPLACE)

             OVRDBF     FILE(QADSPOBJ) TOFILE(QTEMP/QADSPOBJ) +
                          OVRSCOPE(*CALLLVL)

 NXTOBJ:     RCVF       RCDFMT(QLIDOBJD) OPNID(A)
                        MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ENDOBJ))

             CHGOBJOWN  OBJ(&A_ODLBNM/&A_ODOBNM) OBJTYPE(&A_ODOBTP) +
                          NEWOWN(&USRPRF)

             GRTOBJAUT  OBJ(&A_ODLBNM/&A_ODOBNM) OBJTYPE(&A_ODOBTP) +
                          USER(*PUBLIC) AUT(*CHANGE) REPLACE(*YES)
                        MONMSG     MSGID(CPF0000)

             GOTO       CMDLBL(NXTOBJ)

 ENDOBJ:

/******************************************************************************/
/*  COPY SOURCE TO ROOT FILE SYSTEM.                                          */
/******************************************************************************/

             CHGVAR     VAR(&SRCPTH) VALUE('/' *TCAT &INSLIB)

             MKDIR      DIR(&SRCPTH) DTAAUT(*RWX) OBJAUT(*ALL)
                        MONMSG     MSGID(CPF0000)

             DSPFD      FILE(&INSLIB/Q*) TYPE(*MBRLIST) +
                          OUTPUT(*OUTFILE) OUTFILE(QTEMP/QAFDMBRL) +
                          OUTMBR(*FIRST *REPLACE)

             OVRDBF     FILE(QAFDMBRL) TOFILE(QTEMP/QAFDMBRL)

 NXTMBR:     RCVF       RCDFMT(QWHFDML) OPNID(B)
                        MONMSG     MSGID(CPF0000) EXEC(GOTO CMDLBL(ENDMBR))

             IF         COND(&B_MLFILE *NE &SRCSAV) THEN(DO)

                        CHGVAR     VAR(&SRCPTH) VALUE('/' *TCAT &INSLIB *TCAT +
                                     '/' *TCAT &B_MLFILE)

                        MKDIR      DIR(&SRCPTH) DTAAUT(*RWX) OBJAUT(*ALL)
                                   MONMSG     MSGID(CPF0000)

                        CHGVAR     VAR(&SRCSAV) VALUE(&B_MLFILE)

                        ENDDO

             CHGVAR     VAR(&LIBPTH) VALUE('/QSYS.LIB/' *TCAT &B_MLLIB +
                          *TCAT '.LIB/' *TCAT &B_MLFILE *TCAT +
                          '.FILE/' *TCAT &B_MLNAME *TCAT '.MBR')

             CHGVAR     VAR(&MBRPTH) VALUE(&SRCPTH *TCAT '/' *TCAT +
                          &B_MLNAME *TCAT '.' *TCAT &B_MLSEU2)

             CPYTOSTMF  FROMMBR(&LIBPTH) TOSTMF(&MBRPTH) +
                          STMFOPT(*REPLACE) STMFCODPAG(*PCASCII)
                        MONMSG     MSGID(CPF0000)


             GOTO       CMDLBL(NXTMBR)

 ENDMBR:


/******************************************************************************/
/*  SET CURRENT LIBRARY BACK TO ORIGINAL VALUE.                               */
/******************************************************************************/

             CHGCURLIB  CURLIB(&CURLIB)
                        MONMSG     MSGID(CPF0000) EXEC(CHGCURLIB +
                          CURLIB(*CRTDFT))

             RMVLIBLE   LIB(&INSLIB)
                        MONMSG     MSGID(CPF0000)

             CHGVAR     VAR(&MSGTXT) VALUE('STDXREF successfully +
                          installed to library' *BCAT &INSLIB *TCAT +
                          '.')

 EXIT:       SNDPGMMSG  MSG(&MSGTXT)

             ENDPGM

