     A**************************************************************************
     A*
     A*  STANDARD CROSS REFERENCE MAINTENANCE
     A*
     A*  Copyright (C) 2006, 2018  Christopher F. Burns Sr.
     A*    c/o The Inuendo Project (http://inuendo.us).
     A*
     A*  This program is free software: you can redistribute it and/or modify
     A*  it under the terms of the GNU General Public License as published by
     A*  the Free Software Foundation, either version 3 of the License, or
     A*  (at your option) any later version.
     A*
     A*  This program is distributed in the hope that it will be useful,
     A*  but WITHOUT ANY WARRANTY; without even the implied warranty of
     A*  MERCHANTABILITY or FITNESS FOR A PARTICULAR PURPOSE.  See the
     A*  GNU General Public License for more details.
     A*
     A*  You should have received a copy of the GNU General Public License
     A*  along with this program.  If not, see <http://www.gnu.org/licenses/>.
     A*
     A**************************************************************************
     A*
     A                                      DSPSIZ(24 80 *DS3)
     A                                      INDARA
     A*
     A*  REFERENCE TABLE SUBFILE
     A*
     A          R STDXREFS1                 SFL
     A                                      SFLNXTCHG
     A            S1ENTITYID    10Y 0H
     A            S1OPTION       1A  B  6  8COLOR(YLW)
     A N34                                  DSPATR(PR)
     A N34                                  DSPATR(ND)
     A                                      VALUES(' ' 'P' 'T' 'X')
     A            S1REFTBL      10A  O  6 12
     A            S1TBLDES      50A  O  6 24
     A*
     A          R STDXREFC1                 SFLCTL(STDXREFS1)
     A                                      CA03(03 'Exit')
     A                                      CF06(06 'Create')
     A                                      OVERLAY
     A  31                                  SFLDSP
     A  31                                  SFLDSPCTL
     A N31                                  SFLCLR
     A  31                                  SFLEND(*MORE)
     A                                      SFLSIZ(0750)
     A                                      SFLPAG(0015)
     A                                  1  2'Options:'
     A                                  1 12'X=Select'
     A                                  1 22'P=Print'
     A                                  1 31'T=Table'
     A                                  1 60'Standard X-Ref Maint'
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A            C1PGMNAM      10A  O  2 60
     A                                  2 72TIME
     A                                  3  2'Posn To:'
     A            C1POSNTO      10A  B  3 12COLOR(YLW)
     A            C1USRPRF      10A  O  3 60
     A                                  3 72DATE
     A                                      EDTCDE(Y)
     A                                  5  8'Op'
     A                                      DSPATR(UL)
     A                                  5 12'Xref Table'
     A                                      DSPATR(UL)
     A                                  5 24'Description                       -
     A                                                      '
     A                                      DSPATR(UL)
     A*
     A          R STDXREFK1
     A                                      OVERLAY
     A                                 24  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 24 33'F6=Create'
     A                                      COLOR(BLU)
     A                                 24 66'ENTER=Continue'
     A                                      COLOR(BLU)
     A*
     A          R STDXREFW1
     A                                      CA12(12 'Cancel')
     A                                      CF23(23 'Delete')
     A                                      WINDOW(4 10 4 62 *NOMSGLIN)
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      USRRSTDSP
     A            W1ENTITYID    10Y 0H
     A                                  1  1'Xref Table'
     A                                      DSPATR(UL)
     A                                  1 13'Description                       -
     A                                                      '
     A                                      DSPATR(UL)
     A            W1REFTBL      10A  B  2  1
     A  36                                  COLOR(YLW)
     A  36                                  DSPATR(UL)
     A N36                                  DSPATR(PR)
     A  36 69                               ERRMSG('Both a table name and descr-
     A                                      iption are required' 69)
     A  36 67                               ERRMSG('Table name already exists' -
     A                                      67)
     A            W1TBLDES      50A  B  2 13
     A  37                                  COLOR(YLW)
     A  37                                  DSPATR(UL)
     A N37                                  DSPATR(PR)
     A                                  4  1'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  4 25'F23=Delete'
     A                                      COLOR(BLU)
     A                                  4 49'ENTER=Continue'
     A                                      COLOR(BLU)
     A*
     A*  REFERENCE TABLE ENTRY SUBFILE & EDIT WINDOW
     A*
     A          R STDXREFS2                 SFL
     A                                      SFLNXTCHG
     A            S2ENTITYID    10Y 0H
     A                                  6  2' '
     A            S2OPTION       1A  B  7  2COLOR(YLW)
     A N44                                  DSPATR(PR)
     A N44                                  DSPATR(ND)
     A                                      VALUES(' ' 'X')
     A                                  7  5'Arg:'
     A N44                                  DSPATR(ND)
     A            S2NUMARG      10Y 0O  7 16
     A                                      EDTCDE(3)
     A N45                                  DSPATR(ND)
     A            S2CHARARG     15A  O  7 28
     A                                  7 69'Hidden:'
     A N44                                  DSPATR(ND)
     A            S2HIDDEN       1A  O  7 77
     A                                  8  5'Res:'
     A N44                                  DSPATR(ND)
     A            S2NUMRES      15Y 5O  8 10EDTCDE(3)
     A N48                                  DSPATR(ND)
     A            S2CHARRES     50A  O  8 28
     A*
     A          R STDXREFC2                 SFLCTL(STDXREFS2)
     A                                      SFLSIZ(0300)
     A                                      SFLPAG(0005)
     A                                      CA03(03 'Exit')
     A                                      CF06(06 'Create')
     A                                      CA12(12 'Cancel')
     A                                      CF23(23 'Delete')
     A                                      CF21(21 'Print')
     A                                      OVERLAY
     A  41                                  SFLDSP
     A  41                                  SFLDSPCTL
     A N41                                  SFLCLR
     A  41                                  SFLEND(*MORE)
     A                                  1  2'Table:'
     A            C2REFTBL      10A  O  1 10
     A                                  1 60'Standard X-Ref Maint'
     A                                      DSPATR(RI)
     A                                      COLOR(BLU)
     A                                  2  2'Desc:'
     A            C2TBLDES      48A  O  2 10
     A            C2PGMNAM      10A  O  2 60
     A                                  2 72TIME
     A                                  3  2'Option:'
     A                                  3 10'X=Select'
     A            C2USRPRF      10A  O  3 60
     A                                  3 72DATE
     A                                      EDTCDE(Y)
     A                                  5  2'Op'
     A                                      DSPATR(UL)
     A                                  5 10'         Numeric'
     A                                      DSPATR(UL)
     A                                  5 28'Character                         -
     A                                                      '
     A                                      DSPATR(UL)
     A*
     A          R STDXREFK2
     A                                      OVERLAY
     A                                      CA03(03 'Exit')
     A                                      CF06(06 'Create')
     A                                      CA12(12 'Cancel')
     A                                      CF23(23 'Delete')
     A                                 24  2'F3=Exit'
     A                                      COLOR(BLU)
     A                                 24 13'F6=Create'
     A                                      COLOR(BLU)
     A                                 24 26'F12=Cancel'
     A                                      COLOR(BLU)
     A                                 24 39'F21=Print'
     A                                      COLOR(BLU)
     A                                 24 52'F23=Delete'
     A                                      COLOR(BLU)
     A                                 24 66'ENTER=Continue'
     A                                      COLOR(BLU)
     A*
     A          R STDXREFW2
     A                                      WINDOW(4 3 6 73 *NOMSGLIN)
     A                                      CA12(12 'Cancel')
     A                                      CF23(23 'Delete')
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      USRRSTDSP
     A            W2ENTITYID    10Y 0H
     A                                  1  6'         Numeric'
     A                                      DSPATR(UL)
     A                                  1 24'Character                         -
     A                                                      '
     A                                      DSPATR(UL)
     A                                  3  1'Arg:'
     A            W2NUMARG      10Y 0B  3 12EDTCDE(4)
     A  46                                  COLOR(YLW)
     A  46                                  DSPATR(UL)
     A N46                                  DSPATR(PR)
     A  46 77                               ERRMSG('An entry with these argumen-
     A                                      ts already exists in this table' 77)
     A            W2CHARARG     15A  B  3 24
     A  46                                  COLOR(YLW)
     A  46                                  DSPATR(UL)
     A N46                                  DSPATR(PR)
     A  46 76                               ERRMSG('Reserved word cannot be use-
     A                                      d as character argument' 76)
     A                                  3 65'Hidden:'
     A  46                                  DSPATR(ND)
     A            W2HIDDEN       1A  B  3 73
     A                                      VALUES('N' 'Y')
     A  47                                  COLOR(YLW)
     A  47                                  DSPATR(UL)
     A  46                                  DSPATR(ND)
     A  46                                  DSPATR(PR)
     A                                  4  1'Res:'
     A            W2NUMRES      15Y 5B  4  6EDTCDE(4)
     A  47                                  COLOR(YLW)
     A  47                                  DSPATR(UL)
     A N47                                  DSPATR(PR)
     A            W2CHARRES     50A  B  4 24
     A  47                                  COLOR(YLW)
     A  47                                  DSPATR(UL)
     A N47                                  DSPATR(PR)
     A                                  6  1'F12=Cancel'
     A                                      COLOR(BLU)
     A                                  6 30'F23=Delete'
     A                                      COLOR(BLU)
     A                                  6 60'ENTER=Continue'
     A                                      COLOR(BLU)
     A*
     A*  CONFIRMATION THAT A REFERENCE TABLE WAS PRINTED
     A*
     A          R STDXREFW3
     A                                      WINDOW(10 25 4 28 *NOMSGLIN)
     A                                      WDWBORDER((*COLOR BLU) (*DSPATR RI)-
     A                                       (*CHAR '        '))
     A                                      USRRSTDSP
     A            W3REFTBL      10A  O  2  7
     A                                      DSPATR(HI UL)
     A                                  2  1'Table'
     A                                  2 18'was printed'
     A                                  4 15'ENTER=Continue'
     A                                      COLOR(BLU)
     A*
